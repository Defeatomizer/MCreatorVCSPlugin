import groovy.json.JsonSlurper

plugins {
    id "java"
    id "idea"
}

repositories {
    mavenCentral()
    maven { url "https://repo.gradle.org/gradle/libs-releases" }
    flatDir { dirs mcreator_path + "/lib" }
}

group = 'net.mcreator.vcs'
version = new JsonSlurper().parse(file('src/main/resources/plugin.json'))['info']['version']

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

idea {
    module {
        inheritOutputDirs = true

        // define exclude dirs
        excludeDirs += file(".github")
        excludeDirs += file(".gradle")
        excludeDirs += file(".idea")
        excludeDirs += file("build")
        excludeDirs += file("gradle")
        excludeDirs += file("jdk")
        excludeDirs += file("license")
        excludeDirs += file("logs")
    }
}

configurations {
    implementation.extendsFrom export
}

dependencies {
    implementation project(':MCreator')
    project(':MCreator').afterEvaluate(() -> project(':MCreator').configurations.implementation.dependencies.each {
        //noinspection ForeignDelegate
        implementation it
    })
    export group: 'org.openl.jgit', name: 'org.eclipse.jgit', version: '6.9.0.202403050737-openl'
    export group: 'org.openl.jgit', name: 'org.eclipse.jgit.gpg.bc', version: '6.8.0.202311291450-openl'
    export group: 'org.openl.jgit', name: 'org.eclipse.jgit.ssh.apache', version: '6.8.0.202311291450-openl'
}

tasks.jar {
    archiveFileName.set('mcreator-vcs-' + project.version + '.zip')

    delete 'build/export/', 'build/libs/'
    mkdir 'build/export/'

    from 'LICENSE.txt'
    from('license') { into 'license' }

    configurations.export.each {
        from((it.isDirectory() ? fileTree(it) : zipTree(it)).matching {
            exclude 'META-INF/**', 'OSGI-INF/**', 'about.html'
        })
    }

    from(mergeServiceProviders(configurations.export.collect {
        (it.isDirectory() ? fileTree(it) : zipTree(it)).matching {
            include 'META-INF/services/**'
        }
    })) { into 'META-INF/services' }

    doLast {
        delete 'build/export/', 'build/tmp/.cache/'
    }
}

tasks.register('runMCreatorWithPlugin', JavaExec) {
    dependsOn jar

    environment("MCREATOR_PLUGINS_FOLDER", file("./build/libs"))
    environment("MCREATOR_PLUGINS_DEV", "")

    jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'

    classpath = project(':MCreator').sourceSets.main.runtimeClasspath + sourceSets.main.runtimeClasspath
    mainClass.set('net.mcreator.Launcher')

    workingDir = mcreator_path
}

private List<File> mergeServiceProviders(Iterable<FileTree> files, String parentFolder = 'export/services/') {
    def services = new HashMap<String, List<String>>()
    files.each { it.each {
        def providers = services.computeIfAbsent(it.name) { new ArrayList<>() }
        it.eachLine {
            if (it.matches "(\\p{javaJavaIdentifierStart}\\p{javaJavaIdentifierPart}*\\.)*\\p{javaJavaIdentifierStart}\\p{javaJavaIdentifierPart}*")
                providers.add(it)
            return it
        }
    } }

    def base = new File(layout.buildDirectory.asFile.get(), parentFolder)
    base.mkdirs()
    return services.collect {
        def merged = new File(base, it.key)
        merged.text = String.join(System.lineSeparator(), it.value)
        return merged
    }
}